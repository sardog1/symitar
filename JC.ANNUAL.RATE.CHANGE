[JC.ANNUAL.CC.RATE.CHANGE

 DESIGNED VIA TICKET #15425

]


TARGET=LOAN


DEFINE
 LASTFICO=NUMBER
 LASTFICODATE=DATE
 NEWGRADE=NUMBER
 OLDGRADE=NUMBER
 LTYPE=NUMBER
 NEWLTYPE=NUMBER
 VALIDTYPES=NUMBER ARRAY(99)

 GRADEMOVE=NUMBER ARRAY(99)
 #INCLUDE "RD.OUTPUT.DEF"
 OUTPUTCHANNEL		=			NUMBER
 ERRORTEXT			=			CHARACTER
 FMCHANNEL			=			NUMBER
 BEGDATE			=		DATE
 ENDDATE			=		DATE
 TEMPDATE			=		DATE
 XACCOUNT			=		CHARACTER
 MINDATE			=		DATE

END

SETUP
 
 VALIDTYPES(70)=1
 VALIDTYPES(73)=1
 VALIDTYPES(74)=1 
 VALIDTYPES(75)=1
 VALIDTYPES(76)=1
 VALIDTYPES(77)=1
 VALIDTYPES(78)=1 
 VALIDTYPES(80)=1
 VALIDTYPES(81)=1
 VALIDTYPES(82)=1
 VALIDTYPES(83)=1
 VALIDTYPES(84)=1
 VALIDTYPES(85)=0
 VALIDTYPES(90)=1
 VALIDTYPES(91)=1
 VALIDTYPES(92)=1
 VALIDTYPES(93)=1
 VALIDTYPES(94)=1
 BEGDATE=DATEOFFSET(SYSTEMDATE,-1,28)+1
 ENDDATE=SYSTEMDATE


 GRADEMOVE(74)=0
 GRADEMOVE(75)=74
 GRADEMOVE(76)=75
 GRADEMOVE(77)=76
 GRADEMOVE(78)=77
 GRADEMOVE(73)=76

 GRADEMOVE(82)=80
 GRADEMOVE(81)=82
 GRADEMOVE(83)=81
 GRADEMOVE(84)=83
 GRADEMOVE(85)=81

 GRADEMOVE(91)=90
 GRADEMOVE(92)=91
 GRADEMOVE(93)=92
 GRADEMOVE(94)=93
 GRADEMOVE(99)=92



END



SELECT
 LOAN:CLOSEDATE='--/--/----' AND LOAN:CHARGEOFFDATE='--/--/----' AND
 NOT (LOAN:BALANCE<>$0.00 AND LOAN:DUEDATE<SYSTEMDATE)  AND VALIDTYPES(LOAN:TYPE)=1
END

PRINT TITLE="UPGRADE CC RATES"
 [FIRST, IS IT DUE? DATES ARE FROM THE DATE AFTER THE 28TH PREVIOUS TO THE SYSTEMDATE]
 
  [NOTE OPENDATE COULD BE 1/1/2022 OR 12/31/2021 WHICH MAKES THE MATH TRICKY
   BEG AND ENDDATE CALCS ABOVE SHOULD FIGURE OUT THE RANGE BUT
   WE NEED TO SEE IF THE MONTH/YEAR MATCH UP]

   IF LOAN:TYPE=73 OR LOAN:TYPE=99 OR LOAN:TYPE=85 THEN MINDATE=DATEOFFSET(SYSTEMDATE,-24,0) ELSE MINDATE=DATEOFFSET(SYSTEMDATE,-12,0)

   IF MONTH(LOAN:OPENDATE)=12 AND DAY(LOAN:OPENDATE)>28 THEN TEMPDATE=DATE(MONTH(LOAN:OPENDATE),DAY(LOAN:OPENDATE),YEAR(SYSTEMDATE)-1)
   ELSE TEMPDATE=DATE(MONTH(LOAN:OPENDATE),DAY(LOAN:OPENDATE),YEAR(SYSTEMDATE))

[  12/21/1900 - 12/21/2022 --> 12/28/2022 RUN
  12/29/1900 - 12/29/2021 --> 1/28/2022 RUN
  3/15/1900 - 3/15/2022 -- 3/28/2022 RUN]

 
 LTYPE=LOAN:TYPE

  IF TEMPDATE>=BEGDATE AND TEMPDATE<=ENDDATE AND LOAN:OPENDATE>=MINDATE THEN DO
 	 CALL GETFICO
	 CALL UPGRADE
	 IF LASTFICODATE>SYSTEMDATE-180 AND NEWLTYPE<>0 THEN DO
		CALL FMLOAN
		CALL CREATENOTE
		[WILL PUT LETTER INTO JC.LETTER.GEN]
	 END
  END


END


PROCEDURE UPGRADE

CALL GETOLDGRADE
CALL GETNEWGRADE

IF NEWGRADE<OLDGRADE THEN DO
	[CAN MOVE UP AT MOST ONE]
      NEWLTYPE=GRADEMOVE(LTYPE)
END

     
END

PROCEDURE GETFICO
	FOR ACCOUNT ACCOUNT:NUMBER DO
		LASTFICO=0
		LASTFICODATE='--/--/----'
		FOR EACH TRACKING WITH TRACKING:TYPE=40 DO
			LASTFICODATE=TRACKING:USERDATE1
			LASTFICO=TRACKING:USERNUMBER1
		END UNTIL LASTFICO>0
	END
END

PROCEDURE FMLOAN
	PRINT ACCOUNT:NUMBER
	PRINT " MODIFY LOAN "
	PRINT LOAN:ID
	NEWLINE
	PRINT "CHANGE TYPE FROM "
	PRINT LOAN:TYPE
	PRINT " TO "
	PRINT NEWLTYPE
	NEWLINE
	OUTPUTSWITCH(OUTPUTCHANNELDEFAULT,ERRORTEXT)
END

PROCEDURE CREATENOTE

	PRINT "ACCOUNT "
	PRINT ACCOUNT:NUMBER
	PRINT " CREATE LOAN  "
	PRINT LOAN:ID
	PRINT " NOTE BEFOREFIRST"

	NEWLINE
	PRINT "SET TEXT:1 TO "
	COL=49 "NOTICE OF UPGRADE"
	NEWLINE
	PRINT "SET TEXT:2 TO "
	COL=49 "CHANGED TYPE FROM "+FORMAT("9999",LOAN:TYPE)+" TO "+FORMAT("9999",NEWLTYPE)
	NEWLINE
END

PROCEDURE GETNEWGRADE
  NEWGRADE=0
  IF LASTFICODATE>=SYSTEMDATE-180 THEN DO
  IF LASTFICO>=730 THEN NEWGRADE=1
  IF LASTFICO>=690 AND NEWGRADE=0 THEN NEWGRADE=2
  IF LASTFICO>=660 AND NEWGRADE=0 THEN NEWGRADE=3
  IF LASTFICO>=620 AND NEWGRADE=0 THEN NEWGRADE=4
  IF LASTFICO>=570 AND NEWGRADE=0 THEN NEWGRADE=5
  IF LASTFICO<570 THEN NEWGRADE=6
 END

END

PROCEDURE GETOLDGRADE
  OLDGRADE=0
  IF LTYPE=74 OR LTYPE=80 OR LTYPE=70 OR LTYPE=90 THEN OLDGRADE=1	[A+]
  IF LTYPE=75 OR LTYPE=82 OR LTYPE=91 then OLDGRADE=2  [A]
  IF LTYPE=76 OR LTYPE=81 OR LTYPE=92 THEN OLDGRADE=3  [B]
  IF LTYPE=77 OR LTYPE=83 OR LTYPE=93 OR LTYPE=77 OR LTYPE=85 OR LTYPE=99 THEN OLDGRADE=3  [C]
  IF LTYPE=78 OR LTYPE=84 THEN OLDGRADE=4  [D]
END
